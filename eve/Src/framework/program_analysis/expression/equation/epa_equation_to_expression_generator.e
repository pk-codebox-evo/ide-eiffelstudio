note
	description: "Summary description for {AFX_EQUATION_TO_EXPRESSION_GENERATOR}."
	author: ""
	date: "$Date$"
	revision: "$Revision$"

class
	EPA_EQUATION_TO_EXPRESSION_GENERATOR

inherit
	EPA_EXPRESSION_VALUE_VISITOR

feature -- Access

	last_expression: detachable EPA_EXPRESSION
			-- Last expression generated by `generate'

feature -- Basic operations

	generate (a_equation: EPA_EQUATION)
			-- Generator expression from `a_equation'
		do
			expression_text := a_equation.expression.text
			class_ := a_equation.class_
			feature_ := a_equation.feature_
			written_class := a_equation.expression.written_class
			a_equation.value.process (Current)
		end

feature{NONE} -- Implementation

	expression_text: detachable STRING
			-- Text for expression part of an equation

	template: detachable STRING
			-- Template for the final expression

	place_holder (a_tag: STRING): STRING
			-- Place holder with `a_tag'.
		do
			create Result.make (a_tag.count + 4)
			Result.append (once "{{")
			Result.append (a_tag)
			Result.append (once "}}")
		end

	expression_place_holder: STRING = "{{e}}"

	value_place_holder: STRING = "{{v}}"

	class_: CLASS_C
			-- Context class

	feature_: detachable FEATURE_I
			-- Context feature

	written_class: CLASS_C
			-- Written class

feature -- Process

	process_deterministic_value (a_value: EPA_EXPRESSION_VALUE)
				-- Process `a_value'
		require
			a_value_is_deterministic: a_value.is_deterministic
		local
			l_text: STRING
		do
			l_text := expression_text + " = " + a_value.item.out
			create {EPA_AST_EXPRESSION} last_expression.make_with_text (class_, feature_, l_text, written_class)
		end

	process_boolean_value (a_value: EPA_BOOLEAN_VALUE)
			-- Process `a_value'.
		do
			process_deterministic_value (a_value)
		end

	process_random_boolean_value (a_value: EPA_RANDOM_BOOLEAN_VALUE)
			-- Process `a_value'.
		local
			l_text: STRING
		do
			l_text := "(" + expression_text + ") or not (" + expression_text + ")"
			create {EPA_AST_EXPRESSION} last_expression.make_with_text (class_, feature_, l_text, written_class)
		end

	process_integer_value (a_value: EPA_INTEGER_VALUE)
			-- Process `a_value'.
		do
			process_deterministic_value (a_value)
		end

	process_pointer_value (a_value: EPA_POINTER_VALUE)
			-- Process `a_value'.
		do
			process_deterministic_value (a_value)
		end

	process_real_value (a_value: EPA_REAL_VALUE)
			-- Process `a_value'.
		do
			process_deterministic_value (a_value)
		end

	process_random_integer_value (a_value: EPA_RANDOM_INTEGER_VALUE)
			-- Process `a_value'.
		local
			l_text: STRING
		do
			l_text := "(" + expression_text + " >= " + min_integer_value.out + ") and (" + expression_text + " <= " + max_integer_value.out + ")"
			create {EPA_AST_EXPRESSION} last_expression.make_with_text (class_, feature_, l_text, written_class)
		end

	process_nonsensical_value (a_value: EPA_NONSENSICAL_VALUE)
			-- Process `a_value'.
		do
			check should_not_happen: False end
		end

	process_void_value (a_value: EPA_VOID_VALUE)
			-- Process `a_value'.
		do
			check should_not_happen: False end
		end

	process_any_value (a_value: EPA_ANY_VALUE)
			-- Process `a_value'.
		do
			process_deterministic_value (a_value)
		end

	process_reference_value (a_value: EPA_REFERENCE_VALUE)
			-- Process `a_value'.
		do
			process_deterministic_value (a_value)
		end

	process_ast_expression_value (a_value: EPA_AST_EXPRESSION_VALUE)
			-- Process `a_value'.
		do
		end

	process_string_value (a_value: EPA_STRING_VALUE)
			-- Process `a_value'
		do
		end

	process_set_value (a_value: EPA_EXPRESSION_SET_VALUE)
			-- Process `a_value'.
		do
		end

	process_numeric_range_value (a_value: EPA_NUMERIC_RANGE_VALUE)
			-- Process `a_value'.
		do
		end

feature{NONE} -- Implementation

	max_integer_value: INTEGER
			-- Max integer value
		do
			Result := {INTEGER_32}.max_value
		end

	min_integer_value: INTEGER
			-- Min integer value
		do
			Result := {INTEGER_32}.min_value
		end

end
